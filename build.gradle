plugins {
	id 'org.springframework.boot' version '2.4.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'com.github.spotbugs' version '4.6.0'
	id 'org.owasp.dependencycheck' version '6.1.0'
}

group = 'uk.nhs.prm.deductions'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '14'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-activemq'
	implementation 'net.logstash.logback:logstash-logback-encoder:6.4'
//	implementation 'org.springframework.boot:spring-boot-starter-log4j2:2.4.2'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.1'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.11.1'
	implementation 'org.apache.activemq:activemq-broker:5.16.1'
	implementation 'org.apache.activemq:activemq-client:5.16.1'
	implementation 'com.sun.mail:javax.mail:1.6.2'
	implementation 'com.sun.mail:javax.mail:1.6.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.4.2'
	testImplementation 'org.hamcrest:hamcrest:2.2'
	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
	implementation 'com.fasterxml.woodstox:woodstox-core:5.1.0'
}

//configurations.all {
//	exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
//}

test {
	useJUnitPlatform()
}

task unitTest(type: Test) {
	useJUnitPlatform {
		excludeTags 'integration'
	}
}

task integrationTest(type: Test) {
	useJUnitPlatform {
		excludeTags 'unit'
	}
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

jacocoTestCoverageVerification {
	dependsOn jacocoTestReport

	violationRules {
		rule {
			limit {
				minimum = 0.7
			}
		}
	}
}

spotbugsMain {
	reports {
		html {
			enabled = true
			destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

spotbugsTest {
	reports {
		html {
			enabled = true
			destination = file("$buildDir/reports/spotbugs/test/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

dependencyCheck {
	failBuildOnCVSS=7
	suppressionFile='./dependency-checks-suppression.xml'
}